
<!DOCTYPE html>
<!--[if IEMobile 7 ]><html class="no-js iem7"><![endif]-->
<!--[if lt IE 9]><html class="no-js lte-ie8"><![endif]-->
<!--[if (gt IE 8)|(gt IEMobile 7)|!(IEMobile)|!(IE)]><!--><html class="no-js" lang="en"><!--<![endif]-->
<head>
  <meta charset="utf-8">
  <title>Introducing F# to Experienced Developers - Mavnn's blog</title>
  <meta name="author" content="mavnn">

  
  <meta name="description" content="So, as a follow up to this post I&rsquo;m in the final stages of preparing a presentation for this Friday introducing an audience of (mostly) fairly &hellip;">
  

  <!-- http://t.co/dKP3o1e -->
  <meta name="HandheldFriendly" content="True">
  <meta name="MobileOptimized" content="320">
  <meta name="viewport" content="width=device-width, initial-scale=1">


  <link rel="canonical" href="http://blog.mavnn.co.uk/introducing-f-number-to-experienced-developers/">
  <link href="/favicon.ico" rel="icon">
  <link href="/stylesheets/screen.css" media="screen, projection" rel="stylesheet" type="text/css">
  <link href="/atom.xml" rel="alternate" title="Mavnn's blog" type="application/atom+xml">
  <script src="/javascripts/modernizr-2.0.js"></script>
  <script src="//ajax.googleapis.com/ajax/libs/jquery/1.9.1/jquery.min.js"></script>
  <script>!window.jQuery && document.write(unescape('%3Cscript src="./javascripts/libs/jquery.min.js"%3E%3C/script%3E'))</script>
  <script src="/javascripts/octopress.js" type="text/javascript"></script>
  <!--Fonts from Google"s Web font directory at http://google.com/webfonts -->
<link href="http://fonts.googleapis.com/css?family=PT+Serif:regular,italic,bold,bolditalic" rel="stylesheet" type="text/css">
<link href="http://fonts.googleapis.com/css?family=PT+Sans:regular,italic,bold,bolditalic" rel="stylesheet" type="text/css">

  
  <script type="text/javascript">
    var _gaq = _gaq || [];
    _gaq.push(['_setAccount', 'UA-37561687-1']);
    _gaq.push(['_trackPageview']);

    (function() {
      var ga = document.createElement('script'); ga.type = 'text/javascript'; ga.async = true;
      ga.src = ('https:' == document.location.protocol ? 'https://ssl' : 'http://www') + '.google-analytics.com/ga.js';
      var s = document.getElementsByTagName('script')[0]; s.parentNode.insertBefore(ga, s);
    })();
  </script>


</head>

<body   >
  <header role="banner"><hgroup>
  <h1><a href="/">Mavnn's blog</a></h1>
  
    <h2>Stuff from my brain</h2>
  
</hgroup>

</header>
  <nav role="navigation"><ul class="subscription" data-subscription="rss">
  <li><a href="/atom.xml" rel="subscribe-rss" title="subscribe via RSS">RSS</a></li>
  
</ul>
  
<form action="http://google.com/search" method="get">
  <fieldset role="search">
    <input type="hidden" name="q" value="site:blog.mavnn.co.uk" />
    <input class="search" type="text" name="q" results="0" placeholder="Search"/>
  </fieldset>
</form>
  
<ul class="main-navigation">
  <li><a href="/">Blog</a></li>
  <li><a href="/blog/archives">Archives</a></li>
</ul>

</nav>
  <div id="main">
    <div id="content">
      <div>
<article class="hentry" role="article">
  
  <header>
    
      <h1 class="entry-title">Introducing F# to Experienced Developers</h1>
    
    
      <p class="meta">
        








  


<time datetime="2013-10-14T12:30:00+01:00" pubdate data-updated="true">Oct 14<span>th</span>, 2013</time>
        
      </p>
    
  </header>


<div class="entry-content"><p>So, as a follow up to <a href="" title="http://blog.mavnn.co.uk/teaching-f-number-to-c-number-devs/">this post</a> I&rsquo;m in the final stages of preparing a presentation for this Friday introducing an audience of (mostly) fairly experienced developers to F# and F# syntax. The main reason for this is to get a number of people up to speed enough on reading F# that they can have a better experience at <a href="" title="http://skillsmatter.com/event/scala/progressive-f-tutorials-2013">the Progressive F# Tutorials</a> at the end of the month. So the aim here isn&rsquo;t to get people fully autonomous and writing code <em>right now</em>, but to allow them to read the bulk of the example code in the tutorials and follow what&rsquo;s going on.</p>

<p>The general approach I&rsquo;ve gone for is to set up a Git repository that has a series of tagged snap shots I can check out as I work through the concepts I&rsquo;m planning to cover. This will enable me to actually demonstrate and run pieces of code, answer questions and make live modifications and then always jump back to a known starting point for the next section of the talk. Given the people involved have all done some .net development and I don&rsquo;t need to cover things like Visual Studio usage and projects, all of the code is contained in a single Program.fs file in a console app. I&rsquo;ve included the snapshot of the file from each tagged commit below, with a brief overview of what I&rsquo;m planning to introduce before skipping to the next snapshot.</p>

<p>With a full screen Visual Studio editing session, I should be able to make the code large enough to be visible and reasonably rapidly guide people to the areas where the code has changed.</p>

<p>A combination of the excellent <a href="http://papercut.codeplex.com/">PaperCut</a> project and a local &lsquo;http request to email&rsquo; service pretending to be an SMS sender, we should be able to see messages being generated by the code as we go along.</p>

<p>After the session, I&rsquo;m planning to mention <a href="https://github.com/ChrisMarinos/FSharpKoans">Chris Marinos&#8217; koans</a> and <a href="http://www.tryfsharp.org/">Try F#</a> (especially given that Rachel Reese is <a href="http://skillsmatter.com/podcast/scala/try-f-from-zero-to-data-science">running a session</a> at the tutorials for those who are going).</p>

<p>Please note that these are up here for comment and suggestions at this point &ndash; I&rsquo;ll be pushing up the actual Git repository and a screencast (I hope) after the event. The code is designed to be a prop for the talk rather than an independent resource &ndash; for that I&rsquo;d always point people to the koans/Try F# first.</p>

<p>So, show me the codez:</p>

<!--more-->


<h2>Tag &lsquo;1&rsquo;</h2>

<p>Nothing too exciting here :). This is just a place holder while giving the introduction, although I will also point out the lack of required boiler plate compared to C#.</p>

<div><script src='https://gist.github.com/mavnn/6973457.js?file=part1.fs'></script>
<noscript><pre><code>// Let's send an email!
</code></pre></noscript></div>


<h2>Tag &lsquo;2&rsquo;</h2>

<p>Introduce the <code>open</code> keyword, <code>let</code> for value assignment, and give people a feel that they&rsquo;re not completely leaving their nice safe .net world behind.</p>

<div><script src='https://gist.github.com/mavnn/6973457.js?file=part2.fs'></script>
<noscript><pre><code>// Let's send an email!

open System.Net.Mail

// We only use the 'new' keyword here because SmtpClient is disposable
let smtpClient = new SmtpClient(&quot;smtp.local&quot;)

// And given it's disposable, we should really dispose of it...
smtpClient.Dispose()</code></pre></noscript></div>


<h2>Tag &lsquo;3&rsquo;</h2>

<p>Talk about functions, show parameter application, introduce the pipe operator.</p>

<p>Some discussion about type inference will probably happen here.</p>

<div><script src='https://gist.github.com/mavnn/6973457.js?file=part3.fs'></script>
<noscript><pre><code>open System.Net.Mail

let smtpClient = new SmtpClient(&quot;smtp.local&quot;)

// This is a function
let sendMessage client message =
    // We'd better do something here to actually
    // send a message...
    printfn &quot;I haven't sent a mail message!&quot;
    ()

sendMessage smtpClient &quot;My message&quot;

smtpClient.Dispose()

// And we'll add this so we can see the output
// before it disappears
System.Console.ReadLine() |&gt; ignore
// This line would be the same as writing:
//
// ignore (System.Console.Readline())
//
// but you have to admit that this is a bit 
// more readable</code></pre></noscript></div>


<h2>Tag &lsquo;4&rsquo;</h2>

<p>Introduce the <code>use</code> keyword, show property assignment with <code>&lt;-</code>.</p>

<div><script src='https://gist.github.com/mavnn/6973457.js?file=part4.fs'></script>
<noscript><pre><code>open System.Net.Mail

let smtpClient = new SmtpClient(&quot;smtp.local&quot;)

let sendMessage client message =
    (* Because we used 'use' this will get
    disposed at the end of the declaring
    scope *)
    use mailMessage = new MailMessage(&quot;me@example.com&quot;, &quot;you@example.com&quot;)
    (* This is have you assign a parameter *)
    mailMessage.Subject &lt;- &quot;Message subject&quot;
    mailMessage.Body &lt;- message
    smtpClient.Send mailMessage
    printfn &quot;I've sent a mail message!&quot;

sendMessage smtpClient &quot;My message&quot;

smtpClient.Dispose()
System.Console.ReadLine() |&gt; ignore
</code></pre></noscript></div>


<h2>Tag &lsquo;5&rsquo;</h2>

<p>List syntax and introduce the <code>Seq</code> module. Example of currying.</p>

<div><script src='https://gist.github.com/mavnn/6973457.js?file=part5.fs'></script>
<noscript><pre><code>open System.Net.Mail

let smtpClient = new SmtpClient(&quot;smtp.local&quot;)

let sendMessage client message =
    use mailMessage = new MailMessage(&quot;me@example.com&quot;, &quot;you@example.com&quot;)
    mailMessage.Subject &lt;- &quot;Message subject&quot;
    mailMessage.Body &lt;- message
    smtpClient.Send mailMessage
    printfn &quot;I've sent a mail message!&quot;

(* But really, what you want computers for
is doing the same thing lots of times... *)
let myMessages =
    [
        &quot;My first message&quot;
        &quot;My second message&quot;
        &quot;My third and final message&quot;
    ]

myMessages
|&gt; Seq.iter (sendMessage smtpClient)
(* Let's have some vindaloo with that map *)

smtpClient.Dispose()
System.Console.ReadLine() |&gt; ignore
</code></pre></noscript></div>


<h2>Tag &lsquo;6&rsquo;</h2>

<p>An async workflow. Turns out that <code>SmtpClient</code> is not as clean for that as you would hope &ndash; it&rsquo;s async send methods don&rsquo;t appear to be thread safe (wait, what?) and even the relatively recent looking <code>SendMailAsync</code> method returns a <code>Task</code> rather than a <code>Task&lt;'T&gt;</code>. Having said that, it shows that even in less than ideal circumstances, you can leverage the <code>async</code> stuff even when interfacing with older .net code from other languages.</p>

<div><script src='https://gist.github.com/mavnn/6973457.js?file=part6.fs'></script>
<noscript><pre><code>open System.Net.Mail


(* But really, what you want computers for
is doing the same thing lots of times... 
at the same time! *)
let sendMessage message =
    async {
        // Move the client inside because...
        // have _you_ checked if it's thread safe?
        use smtpClient = new SmtpClient(&quot;smtp.local&quot;)
        use mailMessage = new MailMessage(&quot;me@example.com&quot;, &quot;you@example.com&quot;)
        mailMessage.Subject &lt;- &quot;Message subject&quot;
        mailMessage.Body &lt;- message
        do!
            smtpClient.SendMailAsync(mailMessage)
            |&gt; Async.AwaitIAsyncResult
            |&gt; Async.Ignore
        printfn &quot;I've sent a mail message!&quot;
    }

let myMessages =
    [
        &quot;My first message&quot;
        &quot;My second message&quot;
        &quot;My third and final message&quot;
    ]

myMessages
|&gt; Seq.map sendMessage
|&gt; Async.Parallel
|&gt; Async.RunSynchronously
|&gt; fun _ -&gt; printfn &quot;Finished all sends!&quot;

System.Console.ReadLine() |&gt; ignore
</code></pre></noscript></div>


<h2>Tag &lsquo;7&rsquo;</h2>

<p>Tuples! Showing both construction and deconstruction syntax. Also discuss that this is how we pass multiple parameters to methods on classes.</p>

<p>In the live coding for this one, I&rsquo;ll make sure to demonstrate adding and removing brackets in different places.</p>

<div><script src='https://gist.github.com/mavnn/6973457.js?file=part7.fs'></script>
<noscript><pre><code>open System.Net.Mail

let sendMessage messageDetails =
    async {
        let address, body = messageDetails
        use smtpClient = new SmtpClient(&quot;smtp.local&quot;)
        use mailMessage = new MailMessage(&quot;me@example.com&quot;, address)
        mailMessage.Subject &lt;- &quot;Message subject&quot;
        mailMessage.Body &lt;- body
        do!
            smtpClient.SendMailAsync(mailMessage)
            |&gt; Async.AwaitIAsyncResult
            |&gt; Async.Ignore
        printfn &quot;I've sent a mail message!&quot;
    }


(* But we probably don't want to send all of these
to the same person. *)
let myMessages =
    [
        &quot;first@example.com&quot;, &quot;My first message&quot;
        &quot;secord@example.com&quot;, &quot;My second message&quot;
        &quot;third@example.com&quot;, &quot;My third and final message&quot;
    ]

myMessages
|&gt; Seq.map sendMessage
|&gt; Async.Parallel
|&gt; Async.RunSynchronously
|&gt; fun _ -&gt; printfn &quot;Finished all sends!&quot;

System.Console.ReadLine() |&gt; ignore
</code></pre></noscript></div>


<h2>Tag &lsquo;8&rsquo;</h2>

<p>Record syntax.</p>

<div><script src='https://gist.github.com/mavnn/6973457.js?file=part8.fs'></script>
<noscript><pre><code>open System.Net.Mail

(* How about if I want to pass lots of different bits
of information in? *)
type messageDetails =
    {
        toAddress : string
        fromAddress : string
        body : string
    }

let sendMessage messageDetails =
    async {
        use smtpClient = new SmtpClient(&quot;smtp.local&quot;)
        use mailMessage = 
            new MailMessage(
                messageDetails.fromAddress,
                messageDetails.toAddress)
        mailMessage.Subject &lt;- &quot;Message subject&quot;
        mailMessage.Body &lt;- messageDetails.body
        do!
            smtpClient.SendMailAsync(mailMessage)
            |&gt; Async.AwaitIAsyncResult
            |&gt; Async.Ignore
        printfn &quot;I've sent a mail message!&quot;
    }

let myMessages =
    [
        { toAddress = &quot;first@example.com&quot;; fromAddress = &quot;official@example.com&quot;; body = &quot;My first message&quot; }
        { toAddress = &quot;second@example.com&quot;; fromAddress = &quot;personal@example.com&quot;; body = &quot;My second message&quot; }
        { toAddress = &quot;first@example.com&quot;; fromAddress = &quot;official@example.com&quot;; body = &quot;My third message&quot; }
    ]

myMessages
|&gt; Seq.map sendMessage
|&gt; Async.Parallel
|&gt; Async.RunSynchronously
|&gt; fun _ -&gt; printfn &quot;Finished all sends!&quot;

System.Console.ReadLine() |&gt; ignore
</code></pre></noscript></div>


<h2>Tag &lsquo;9&rsquo;</h2>

<p>Use discriminated unions for modeling business inputs. In this case, building a MessageDetails class that can contain the details of either an email or an SMS send request.</p>

<p>Also has a 2nd, maybe slightly more idiomatic implementation of an async workflow.</p>

<p>I&rsquo;m hoping to get at least this far in the session. The rest of it would be nice, but if we get here then I&rsquo;ll be happy I&rsquo;ve covered at least the basics.</p>

<div><script src='https://gist.github.com/mavnn/6973457.js?file=part9.fs'></script>
<noscript><pre><code>open System.IO
open System.Net
open System.Net.Mail

(* But some people have given us mobile
numbers rather than email addresses *)
type EmailDetails =
    {
        toAddress : string
        fromAddress : string
        body : string
    }

type SmsDetails =
    {
        toNumber : string
        fromNumber : string
        message : string
    }

type MessageDetails =
    | Email of EmailDetails
    | Sms of SmsDetails

let sendEmail messageDetails =
    async {
        use smtpClient = new SmtpClient(&quot;smtp.local&quot;)
        use mailMessage = 
            new MailMessage(
                messageDetails.fromAddress,
                messageDetails.toAddress)
        mailMessage.Subject &lt;- &quot;Message subject&quot;
        mailMessage.Body &lt;- messageDetails.body
        do!
            smtpClient.SendMailAsync(mailMessage)
            |&gt; Async.AwaitIAsyncResult
            |&gt; Async.Ignore
        printfn &quot;I've sent a mail message!&quot;
    }

let sendSms messageDetails =
    async {
        let http = HttpWebRequest.Create(&quot;http://sms.local&quot;) :?&gt; HttpWebRequest
        http.Method &lt;- &quot;POST&quot;
        let messagePayload =
            sprintf
                &quot;To: %s\nFrom: %s\nMessage: %s&quot;
                messageDetails.toNumber
                messageDetails.fromNumber
                messageDetails.message
        using 
            (http.GetRequestStream())
            (fun stream -&gt; 
                use sw = new StreamWriter(stream)
                sw.Write(messagePayload))
        let! response = http.GetResponseAsync() |&gt; Async.AwaitTask
        if (response :?&gt; HttpWebResponse).StatusCode &lt;&gt; HttpStatusCode.OK then
            failwith &quot;Http request failed!&quot;
        printfn &quot;I've sent an SMS!&quot;
    }

let sendMessage message =
    match message with
    | Email details -&gt; sendEmail details
    | Sms details -&gt; sendSms details

let myMessages =
    [
        Email { toAddress = &quot;first@example.com&quot;; fromAddress = &quot;official@example.com&quot;; body = &quot;My first message&quot; }
        Email { toAddress = &quot;second@example.com&quot;; fromAddress = &quot;personal@example.com&quot;; body = &quot;My second message&quot; }
        Email { toAddress = &quot;first@example.com&quot;; fromAddress = &quot;official@example.com&quot;; body = &quot;My third message&quot; }
        Sms { toNumber = &quot;+447777123123&quot;; fromNumber = &quot;+447888321321&quot;; message = &quot;Hello by sms&quot; }
        Sms { toNumber = &quot;+447777123124&quot;; fromNumber = &quot;+447888321321&quot;; message = &quot;Hello by sms x2&quot; }
        Sms { toNumber = &quot;+447777123123&quot;; fromNumber = &quot;+447888321321&quot;; message = &quot;Hello by sms x3&quot; }
    ]

myMessages
|&gt; Seq.map sendMessage
|&gt; Async.Parallel
|&gt; Async.RunSynchronously
|&gt; fun _ -&gt; printfn &quot;Finished all sends!&quot;

System.Console.ReadLine() |&gt; ignore
</code></pre></noscript></div>


<h2>Tag &lsquo;10&rsquo;</h2>

<p>Validation with Active patterns.</p>

<div><script src='https://gist.github.com/mavnn/6973457.js?file=part10.fs'></script>
<noscript><pre><code>open System.IO
open System.Net
open System.Net.Mail

type EmailDetails =
    {
        toAddress : string
        fromAddress : string
        body : string
    }

type SmsDetails =
    {
        toNumber : string
        fromNumber : string
        message : string
    }

type MessageDetails =
    | Email of EmailDetails
    | Sms of SmsDetails

(* But what if some people have given us invalid data?

Our SMS sender requires full numbers with national
codes - let's add some validation! *)
let (|ValidSmsRequest|InvalidSmsRequest|) details =
    // Hmm. Bananas. My favourite.
    let regex = System.Text.RegularExpressions.Regex(@&quot;^\+\d\d&quot;)
    if regex.IsMatch(details.toNumber) &amp;&amp; regex.IsMatch(details.fromNumber) then
        ValidSmsRequest details
    else
        InvalidSmsRequest &quot;You must include the +xx country prefix on mobile numbers.&quot;

let sendEmail messageDetails =
    async {
        use smtpClient = new SmtpClient(&quot;smtp.local&quot;)
        use mailMessage = 
            new MailMessage(
                messageDetails.fromAddress,
                messageDetails.toAddress)
        mailMessage.Subject &lt;- &quot;Message subject&quot;
        mailMessage.Body &lt;- messageDetails.body
        do!
            smtpClient.SendMailAsync(mailMessage)
            |&gt; Async.AwaitIAsyncResult
            |&gt; Async.Ignore
        printfn &quot;I've sent a mail message!&quot;
    }

(* We've moved the SMS post logic into this method
without change - no validation here.

We've marked it private so no one else can call it
by mistake *)
let private postSms messageDetails =
    async {
        let http = HttpWebRequest.Create(&quot;http://sms.local&quot;) :?&gt; HttpWebRequest
        http.Method &lt;- &quot;POST&quot;
        let messagePayload =
            sprintf
                &quot;To: %s\nFrom: %s\nMessage: %s&quot;
                messageDetails.toNumber
                messageDetails.fromNumber
                messageDetails.message
        using 
            (http.GetRequestStream())
            (fun stream -&gt; 
                use sw = new StreamWriter(stream)
                sw.Write(messagePayload))
        let! response = http.GetResponseAsync() |&gt; Async.AwaitTask
        if (response :?&gt; HttpWebResponse).StatusCode &lt;&gt; HttpStatusCode.OK then
            failwith &quot;Http request failed!&quot;
        printfn &quot;I've sent an SMS!&quot;
    }

(* And this is where we do our validation *)
let sendSms messageDetails =
    match messageDetails with
    | ValidSmsRequest details -&gt; postSms details
    | InvalidSmsRequest error -&gt; async { printfn &quot;Sms sending error: %s&quot; error }

let sendMessage message =
    match message with
    | Email details -&gt; sendEmail details
    | Sms details -&gt; sendSms details

let myMessages =
    [
        Email { toAddress = &quot;first@example.com&quot;; fromAddress = &quot;official@example.com&quot;; body = &quot;My first message&quot; }
        Email { toAddress = &quot;second@example.com&quot;; fromAddress = &quot;personal@example.com&quot;; body = &quot;My second message&quot; }
        Email { toAddress = &quot;first@example.com&quot;; fromAddress = &quot;official@example.com&quot;; body = &quot;My third message&quot; }
        Sms { toNumber = &quot;+447777123123&quot;; fromNumber = &quot;+447888321321&quot;; message = &quot;Hello by sms&quot; }
        Sms { toNumber = &quot;+447777123124&quot;; fromNumber = &quot;+447888321321&quot;; message = &quot;Hello by sms x2&quot; }
        Sms { toNumber = &quot;+447777123123&quot;; fromNumber = &quot;+447888321321&quot;; message = &quot;Hello by sms x3&quot; }
        Sms { toNumber = &quot;447777123123&quot;; fromNumber = &quot;+447888321321&quot;; message = &quot;I won't be sent!&quot; }
        Sms { toNumber = &quot;+447777123123&quot;; fromNumber = &quot;+ab7888321321&quot;; message = &quot;Neither will I!&quot; }
        Sms { toNumber = &quot;Bob&quot;; fromNumber = &quot;+ab7888321321&quot;; message = &quot;..and I definitely won't!&quot; }
    ]

myMessages
|&gt; Seq.map sendMessage
|&gt; Async.Parallel
|&gt; Async.RunSynchronously
|&gt; fun _ -&gt; printfn &quot;Finished all sends!&quot;

System.Console.ReadLine() |&gt; ignore
</code></pre></noscript></div>


<h2>Tag &lsquo;11&rsquo;</h2>

<p>The extra credit section! I very much doubt I&rsquo;ll get this far in a one hour session, but if I do this is just some fun playing with type providers. Also covers randomness such as:</p>

<ul>
<li>generating infinite sequences using <code>seq</code> expressions</li>
<li>double back tick identifiers</li>
<li>pattern matching with guards</li>
<li>a bit more of a workout of the <code>Seq</code> module</li>
<li>we can have a lot of discussion of lazy evaluation, because this code is just insanely broken without it</li>
</ul>


<div><script src='https://gist.github.com/mavnn/6973457.js?file=part11.fs'></script>
<noscript><pre><code>open System
open System.IO
open System.Net
open System.Net.Mail
open System.Text.RegularExpressions

type EmailDetails =
    {
        toAddress : string
        fromAddress : string
        body : string
    }

type SmsDetails =
    {
        toNumber : string
        fromNumber : string
        message : string
    }

type MessageDetails =
    | Email of EmailDetails
    | Sms of SmsDetails

let (|ValidSmsRequest|InvalidSmsRequest|) details =
    let regex = Regex(@&quot;^\+\d\d&quot;)
    if regex.IsMatch(details.toNumber) &amp;&amp; regex.IsMatch(details.fromNumber) then
        ValidSmsRequest details
    else
        InvalidSmsRequest &quot;You must include the +xx country prefix on mobile numbers.&quot;

let sendEmail messageDetails =
    async {
        use smtpClient = new SmtpClient(&quot;smtp.local&quot;)
        use mailMessage = 
            new MailMessage(
                messageDetails.fromAddress,
                messageDetails.toAddress)
        mailMessage.Subject &lt;- &quot;Message subject&quot;
        mailMessage.Body &lt;- messageDetails.body
        do!
            smtpClient.SendMailAsync(mailMessage)
            |&gt; Async.AwaitIAsyncResult
            |&gt; Async.Ignore
        printfn &quot;I've sent a mail message!&quot;
    }

let private postSms messageDetails =
    async {
        let http = HttpWebRequest.Create(&quot;http://sms.local&quot;) :?&gt; HttpWebRequest
        http.Method &lt;- &quot;POST&quot;
        let messagePayload =
            sprintf
                &quot;To: %s\nFrom: %s\nMessage: %s&quot;
                messageDetails.toNumber
                messageDetails.fromNumber
                messageDetails.message
        using 
            (http.GetRequestStream())
            (fun stream -&gt; 
                use sw = new StreamWriter(stream)
                sw.Write(messagePayload))
        let! response = http.GetResponseAsync() |&gt; Async.AwaitTask
        if (response :?&gt; HttpWebResponse).StatusCode &lt;&gt; HttpStatusCode.OK then
            failwith &quot;Http request failed!&quot;
        printfn &quot;I've sent an SMS!&quot;
    }

let sendSms messageDetails =
    match messageDetails with
    | ValidSmsRequest details -&gt; postSms details
    | InvalidSmsRequest error -&gt; async { printfn &quot;Sms sending error: %s&quot; error }

let sendMessage message =
    match message with
    | Email details -&gt; sendEmail details
    | Sms details -&gt; sendSms details
    
(* And now for something completely different...

Let's send a bunch of actors and celebrities a selection 
of astronomical data. Because, you know. Why not?

If you're running this code at home, you'll need
to install the nuget package from the packages.config
file *)
open FSharp.Data
let FreebaseKey =
    let rec getKey (dir : DirectoryInfo) =
        match dir.EnumerateFiles(&quot;freebase.key&quot;) with
        | files when Seq.isEmpty files -&gt; getKey (dir.Parent)
        | files -&gt; (Seq.head files).OpenText().ReadToEnd().Trim()
    let dir = DirectoryInfo(Directory.GetCurrentDirectory())
    getKey dir

type FreebaseProvider = FreebaseDataProvider&lt;Key=&quot;api key goes here&quot;&gt;

let freebase = FreebaseProvider.GetDataContext()

(* If you don't have an api key you can delete lines
88 to the end of this comment, and uncomment the line below.

It will limit how many times you can run the program
before it starts throwing authentication errors,
though - there's a fairly strict rate limit. *)
//let freebase = FreebaseData.GetDataContext()

let actors =
    freebase.``Arts and Entertainment``.Film.``Film actors``
    |&gt; Seq.filter (fun a -&gt; not &lt;| Seq.isEmpty a.``Film performances``)
    (* You get a (virtual) cookie if you can work out
    why I've added the filter below *)
    |&gt; Seq.filter (fun a -&gt; 
        (a.``Film performances`` |&gt; Seq.head)
            .Film.Name.[0 .. 0]
        |&gt; Regex(&quot;[a-zA-Z]&quot;).IsMatch)
    |&gt; Seq.filter (fun a -&gt; not &lt;| Seq.isEmpty a.``Country of nationality``)
    |&gt; Seq.take 20

let encode (str : string) =
    let clean = Regex(&quot;\W&quot;)
    clean.Replace(str, &quot;-&quot;)

let emailAddresses =
    seq { for actor in actors -&gt; 
            let name = actor.Name |&gt; encode
            let domain =
                (actor.``Film performances`` |&gt; Seq.head).Film.Name
                |&gt; encode
            let countryCode =
                match (actor.``Country of nationality`` |&gt; Seq.head).``ISO Alpha 2`` with
                | alpha when Seq.isEmpty alpha -&gt;
                    &quot;com&quot;
                | alpha when (Seq.head alpha).ToLower() = &quot;us&quot; -&gt;
                    &quot;com&quot;
                | alpha -&gt; sprintf &quot;co.%s&quot; &lt;| (Seq.head alpha).ToLower()
            sprintf &quot;%s@%s.%s&quot;
                name
                domain
                countryCode }

(* We're going to need 20 planets for our 20 celebrities,
so we'll repeat the planets as many times as we need *)
let planets =
    seq {
        while true do
            yield! freebase.``Science and Technology``.Astronomy.Planets
    }

let messages =
    seq { for planet in planets -&gt;
            sprintf &quot;&quot;&quot;Hi there!
We thought you might be interested to know that:

The planet %s has:
%d moons!
An average orbital velocity of %Am/s!

And is also known as:
%s

Regards,

Astro
                &quot;&quot;&quot;
                planet.Name
                (planet.``Orbited by`` |&gt; Seq.length)
                planet.``Average Orbital Speed``
                (planet.``Also known as`` |&gt; String.concat &quot;, &quot;) }

let combineAddressAndMessage (address, message) =
    Email {
        toAddress = address
        fromAddress = &quot;astro@random.org&quot;
        body = message
    }

let myMessages =
    Seq.zip emailAddresses messages
    |&gt; Seq.map combineAddressAndMessage

myMessages
|&gt; Seq.map sendMessage
|&gt; Async.Parallel
|&gt; Async.RunSynchronously
|&gt; fun _ -&gt; printfn &quot;Finished all sends!&quot;

System.Console.ReadLine() |&gt; ignore
</code></pre></noscript></div>



</div>


  <footer>
    <p class="meta">
      
  

<span class="byline author vcard">Posted by <span class="fn">mavnn</span></span>

      








  


<time datetime="2013-10-14T12:30:00+01:00" pubdate data-updated="true">Oct 14<span>th</span>, 2013</time>
      

<span class="categories">
  
    <a class='category' href='/blog/categories/fsharp/'>fsharp</a>, <a class='category' href='/blog/categories/programming/'>programming</a>, <a class='category' href='/blog/categories/teaching/'>teaching</a>
  
</span>


    </p>
    
      <div class="sharing">
  
  <a href="//twitter.com/share" class="twitter-share-button" data-url="http://blog.mavnn.co.uk/introducing-f-number-to-experienced-developers/" data-via="mavnn" data-counturl="http://blog.mavnn.co.uk/introducing-f-number-to-experienced-developers/" >Tweet</a>
  
  
  <div class="g-plusone" data-size="medium"></div>
  
  
</div>

    
    <p class="meta">
      
        <a class="basic-alignment left" href="/being-a-priest-and-programming/" title="Previous Post: Being a priest and programming">&laquo; Being a priest and programming</a>
      
      
        <a class="basic-alignment right" href="/to-infinity/" title="Next Post: To Infinity and Beyond">To Infinity and Beyond &raquo;</a>
      
    </p>
  </footer>
</article>

  <section>
    <h1>Comments</h1>
    <div id="disqus_thread" aria-live="polite"><noscript>Please enable JavaScript to view the <a href="http://disqus.com/?ref_noscript">comments powered by Disqus.</a></noscript>
</div>
  </section>

</div>

<aside class="sidebar">
  
    <section>
  <h1>Recent Posts</h1>
  <ul id="recent_posts">
    
      <li class="post">
        <a href="/good-developer-cvs/">Good Developer CVs (Résumés)</a>
      </li>
    
      <li class="post">
        <a href="/rx-solutions/">RX Solutions</a>
      </li>
    
      <li class="post">
        <a href="/exploring-reactive-extensions/">Exploring Reactive Extensions</a>
      </li>
    
      <li class="post">
        <a href="/persistent-data-structures/">Persistent Data Structures</a>
      </li>
    
      <li class="post">
        <a href="/difficult-vs-impossible/">Difficult vs Impossible</a>
      </li>
    
  </ul>
</section>
<section>
  <h1>Tip the author</h1>
  <p><script data-gittip-username="mavnn"
        data-gittip-widget="button"
        src="//gttp.co/v1.js"></script></p>
</section>

<section>
  <h1>GitHub Repos</h1>
  <ul id="gh_repos">
    <li class="loading">Status updating...</li>
  </ul>
  
  <a href="https://github.com/mavnn">@mavnn</a> on GitHub
  
  <script type="text/javascript">
    $(document).ready(function(){
        if (!window.jXHR){
            var jxhr = document.createElement('script');
            jxhr.type = 'text/javascript';
            jxhr.src = '/javascripts/libs/jXHR.js';
            var s = document.getElementsByTagName('script')[0];
            s.parentNode.insertBefore(jxhr, s);
        }

        github.showRepos({
            user: 'mavnn',
            count: 3,
            skip_forks: true,
            target: '#gh_repos'
        });
    });
  </script>
  <script src="/javascripts/github.js" type="text/javascript"> </script>
</section>



<section class="googleplus">
  <h1>
    <a href="https://plus.google.com/+MichaelNewtonmavnn?rel=author">
      <img src="http://www.google.com/images/icons/ui/gprofile_button-32.png" width="32" height="32">
      Google+
    </a>
  </h1>
</section>



  
</aside>


    </div>
  </div>
  <footer role="contentinfo"><p>
  Copyright &copy; 2015 - mavnn -
  <span class="credit">Powered by <a href="http://octopress.org">Octopress</a></span>
</p>

</footer>
  

<script type="text/javascript">
      var disqus_shortname = 'mavnn';
      
        
        // var disqus_developer = 1;
        var disqus_identifier = 'http://blog.mavnn.co.uk/introducing-f-number-to-experienced-developers/';
        var disqus_url = 'http://blog.mavnn.co.uk/introducing-f-number-to-experienced-developers/';
        var disqus_script = 'embed.js';
      
    (function () {
      var dsq = document.createElement('script'); dsq.type = 'text/javascript'; dsq.async = true;
      dsq.src = '//' + disqus_shortname + '.disqus.com/' + disqus_script;
      (document.getElementsByTagName('head')[0] || document.getElementsByTagName('body')[0]).appendChild(dsq);
    }());
</script>





  <script type="text/javascript">
    (function() {
      var script = document.createElement('script'); script.type = 'text/javascript'; script.async = true;
      script.src = 'https://apis.google.com/js/plusone.js';
      var s = document.getElementsByTagName('script')[0]; s.parentNode.insertBefore(script, s);
    })();
  </script>



  <script type="text/javascript">
    (function(){
      var twitterWidgets = document.createElement('script');
      twitterWidgets.type = 'text/javascript';
      twitterWidgets.async = true;
      twitterWidgets.src = '//platform.twitter.com/widgets.js';
      document.getElementsByTagName('head')[0].appendChild(twitterWidgets);
    })();
  </script>





</body>
</html>
